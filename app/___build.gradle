apply plugin: 'com.android.application'
def globalConf = rootProject.ext

android {
    compileSdkVersion globalConf.compileSdkVersion
    buildToolsVersion globalConf.buildToolsVersion

    compileOptions {
        encoding globalConf.encoding
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode globalConf.versionCode
        versionName globalConf.versionName
        applicationId globalConf.applicationId
        targetSdkVersion globalConf.targetSdkVersion
        minSdkVersion globalConf.minSdkVersion
        multiDexEnabled true
    }

    // Specifies one flavor dimension.
    flavorDimensions "api"
    productFlavors {
        minSdk16 {
            dimension "api"
            versionCode 10000 + android.defaultConfig.versionCode
            minSdkVersion 16
            versionNameSuffix "-minApi16"
        }
    }

    lintOptions {
        abortOnError false
    }

    dexOptions {
        // http://developer.android.com/intl/es/tools/building/building-studio.html#instant-run
        preDexLibraries true
        maxProcessCount 4
        javaMaxHeapSize "2g"
        jumboMode = true
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all { output ->
                if (outputFileName.endsWith('.apk')) {
                    outputFileName = "Formudroid_${output.baseName}_${variant.versionName}.apk"
                }
            }
    }

    buildTypes {
        debug {
            resValue "string", "app_name", "*Formudroid"
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
        release {
            resValue "string", "app_name", "Formudroid"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }


}

dependencies {
    Map<String, String> dependencies = globalConf.commonDependencies

    implementation dependencies.appCompat
    implementation dependencies.cardView
    implementation dependencies.design
    implementation dependencies.constraint_layout
    implementation dependencies.coordinatorlayout
    implementation dependencies.jexl
    implementation dependencies.dagger


    implementation dependencies.commonsLang
    implementation dependencies.commonsLogging

    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.mediarouter:mediarouter:1.1.0'
    minSdk16Implementation 'androidx.multidex:multidex:2.0.1'

    testImplementation "org.mockito:mockito-core:1.10.19"
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3'
    // AndroidJUnitRunner and JUnit Rules
    testImplementation 'androidx.test:core:1.0.0'
//    testImplementation 'androidx.test:runner:1.1.0'
    testImplementation 'androidx.test:rules:1.1.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
}
