apply plugin: 'java-library'
apply plugin: 'maven-publish'

def globalConf = rootProject.ext

archivesBaseName = 'core'
group = globalConf.group
version = globalConf.version

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api sharedDeps

    compileOnly 'com.google.android:android:4.1.1.4'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'com.github.tony19:logback-android:2.0.0'
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "http://servicios.itacyl.es/nexus/repository/internal/"
            def snapshotsRepoUrl = "http://servicios.itacyl.es/nexus/repository/snapshots/"
            url version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = mavenUser
                password = mavenPassword
            }
        }
    }
    publications {
        mavenPublish(MavenPublication) {
            from components.java
            artifactId archivesBaseName
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}