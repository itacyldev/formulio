apply plugin: 'java'
apply plugin: 'maven-publish'

archivesBaseName = 'repo'
group = rootProject.group
version = rootProject.version

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':core')

    compileOnly 'com.google.android:android:4.1.1.4'
    implementation 'org.greenrobot:greendao:3.2.3-patched-SNAPSHOT'
    implementation "org.mini2Dx:android-beans:1.0.0"
    implementation 'commons-io:commons-io:2.6'

    // TODO: these should be drawn through transitive dependencies
    implementation 'org.apache.commons:commons-jexl3:3.1'
    implementation 'org.apache.commons:commons-lang3:3.6'
    implementation 'org.apache.commons:commons-text:1.8'

    testCompileOnly 'com.google.android:android:4.1.1.4'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.0.2"
    testImplementation "org.mini2Dx:android-beans:1.0.0"
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "http://repo.itacyl.es/archiva/repository/internal/"
            def snapshotsRepoUrl = "http://repo.itacyl.es/archiva/repository/snapshots/"
            url version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = mavenUser
                password = mavenPassword
            }
        }
    }
    publications {
        repoCore(MavenPublication) {
            from components.java
            artifactId archivesBaseName
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
        }
    }
}